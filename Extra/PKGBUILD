# Maintainer: Niklas Hedlund <nojan1989@gmail.com>
pkgname=mineman-git
pkgver=1.0
pkgrel=1
arch=(x86_64)
url="https://github.com/nojan1/Mineman"
license=('MIT')
depends=(dotnet-cli docker)
makedepends=(dotnet-sdk)
backup=(opt/mineman/database.db opt/mineman/appsettings.json)
source=($pkgname::git+https://github.com/nojan1/Mineman)
md5sums=('SKIP')

framework=netcoreapp2.0

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$pkgname/Mineman.Web"
	
	dotnet restore
	dotnet publish -c Release -f $framework -o build
}

package() {
	cd "$pkgname"
	
	mkdir -p $pkgdir/usr/lib/systemd/system/
	install -m644 Extra/mineman.service $pkgdir/usr/lib/systemd/system/
	
	mkdir -p $pkgdir/opt/mineman
	cp -r Mineman.Web/build/* $pkgdir/opt/mineman/
	install -m644 Extra/appsettings.json $pkgdir/opt/mineman/
	
	mkdir -p $pkgdir/usr/share/mineman/world
	mkdir -p $pkgdir/usr/share/mineman/properties
	mkdir -p $pkgdir/usr/share/mineman/mods
	mkdir -p $pkgdir/usr/share/mineman/imagezipfiles
}